// fragment sprite shader
precision mediump float;
//#ifdef ES_GLES
precision mediump sampler2DArray;
//#endif
uniform sampler2DArray tex;
uniform float frame;
uniform float frameCount;
uniform vec2 blur;
uniform int swizzler;
uniform float alpha;
const int range = 5;

in vec2 fragTexCoord;

out vec4 finalColor;

void main() 
{
  float first = floor(frame);
  float second = mod(ceil(frame), frameCount);
  float fade = frame - first;
  vec4 color;
  if(blur.x == 0.f && blur.y == 0.f)
  {
    if(fade != 0.f)
      color = mix(
        texture(tex, vec3(fragTexCoord, first)),
        texture(tex, vec3(fragTexCoord, second)), fade);
    else
      color = texture(tex, vec3(fragTexCoord, first));
  }
  else
  {
    color = vec4(0., 0., 0., 0.);
    const float divisor = float(range * (range + 2) + 1);
    for(int i = -range; i <= range; ++i)
    {
      float scale = float(range + 1 - abs(i)) / divisor;
      vec2 coord = fragTexCoord + (blur * float(i)) / float(range);
      if(fade != 0.f)
        color += scale * mix(
          texture(tex, vec3(coord, first)),
          texture(tex, vec3(coord, second)), fade);
      else
        color += scale * texture(tex, vec3(coord, first));
    }
  }
  switch (swizzler) 
  {
    case 0:
      color = color.rgba;
      break;
    case 1:
      color = color.rbga;
      break;
    case 2:
      color = color.grba;
      break;
    case 3:
      color = color.brga;
      break;
    case 4:
      color = color.gbra;
      break;
    case 5:
      color = color.bgra;
      break;
    case 6:
      color = color.gbba;
      break;
    case 7:
      color = color.rbba;
      break;
    case 8:
      color = color.rgga;
      break;
    case 9:
      color = color.bbba;
      break;
    case 10:
      color = color.ggga;
      break;
    case 11:
      color = color.rrra;
      break;
    case 12:
      color = color.bbga;
      break;
    case 13:
      color = color.bbra;
      break;
    case 14:
      color = color.ggra;
      break;
    case 15:
      color = color.bgga;
      break;
    case 16:
      color = color.brra;
      break;
    case 17:
      color = color.grra;
      break;
    case 18:
      color = color.bgba;
      break;
    case 19:
      color = color.brba;
      break;
    case 20:
      color = color.grga;
      break;
    case 21:
      color = color.ggba;
      break;
    case 22:
      color = color.rrba;
      break;
    case 23:
      color = color.rrga;
      break;
    case 24:
      color = color.gbga;
      break;
    case 25:
      color = color.rbra;
      break;
    case 26:
      color = color.rgra;
      break;
    case 27:
      color = vec4(color.b, 0.f, 0.f, color.a);
      break;
    case 28:
      color = vec4(0.f, 0.f, 0.f, color.a);
      break;
  };
  finalColor = color * alpha;
};